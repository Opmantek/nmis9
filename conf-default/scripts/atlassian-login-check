#!/usr/bin/perl
# a small NMIS service helper script for testing authentication in atlassian apps
#
# args: url
# -c expected content (regex, case-insensitive)
# -u, -p username and password
#
# exits with 100 if ok, 
# 0 if the login page was not reachable,
# 10 if its content was invalid,
# 20 if the login submission failed,
# or 30 if the credentials didn't work out

# note: confluence and jira are just different enough to be annoying
# we determine the flavour based on the -j option

our $VERSION = "1.0.0";
use strict;
use WWW::Mechanize;
use Getopt::Std;

my %opts;
die "Usage: $0 [-c content-regex] [-N][-j] {-u username} {-p password} <url>
-N: do NOT check ssl certificates
-j: site is Jira (default: Confluence)
\n\n"
		if (!getopts("c:s:f:u:p:Nj",\%opts) 
				or !$opts{u} 
				or !$opts{p} 
				or @ARGV != 1);

my $isjira = $opts{j};

my $okbody = $opts{c}? qr/$opts{c}/i : 
		$isjira? qr/<meta name="ajs-remote-user"\s+content="[^"]+">/i :  qr/<a id="user-menu-link"/i;
my $url = $ARGV[0];
my $username = $opts{u};
my $password = $opts{p};

# don't die on request errors, we know what we're doing and do check results
my @mechargs = (autocheck => 0);
# disable ssl verification only if requested to
push @mechargs, (ssl_opts => { verify_hostname => 0, SSL_verify_mode => 0 }) if ($opts{N});

my $ua = WWW::Mechanize->new(@mechargs);

# get the first (form or landing) page
my $response = $ua->get($url);
if (!$ua->success)
{
	print "Loading of login page failed: ".$response->status_line."\n";
	exit 0;
}

# atlassian's login form is named in confluence, but id'd in jira
elsif ($isjira && $ua->content !~ /id="login-form"/
			 || !$isjira && $ua->content !~ /name="loginform"/)
{
	print "Login page loaded but has no matching form!\n";
	exit 10;
}

my %fielddata = ( "os_username"    => $username, 
									"os_password"    => $password ) ;
my @formid = $isjira? ("form_id" => "login-form" ) : ("form_name" => "loginform" );

# now submit the login form
$response = $ua->submit_form(@formid, fields => \%fielddata);
if (!$ua->success)
{
	print "Login form submission failed: ".$response->status_line."\n";
	exit 20;
}

if ($response->decoded_content =~ $okbody)
{
	print "Login succeeded.\n";
	exit 100;
}
else
{
	print "Login failed!\n";
	exit 30;
}


