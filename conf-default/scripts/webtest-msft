#!/usr/bin/perl
# a small helper script for testing microsoft federated authentication
#
# args: url
# -c expected content (regex, case-insensitive)
# -s expected status code (regex)
# exits with 100 if ok, 10..40 if any of the chained form submissions fails,
# 0 otherwise
use strict;
use WWW::Mechanize;
use Getopt::Std;

my %opts;
die "Usage: $0 [-c content-regex] [-s status-regex] [-N][-d] {-u username} {-p password} <url>
-N: do NOT check ssl certificates
-d: produce debug output on STDERR
\n\n"
		if (!getopts("c:s:f:u:p:Nd",\%opts) or @ARGV != 1);

my $okcode = $opts{s}? qr/$opts{s}/ : qr/200/;
my $okbody = $opts{c} &&  qr/$opts{c}/i;
my $redirs = defined $opts{f}? $opts{f} : 0;
my $url = $ARGV[0];
my $username = $opts{u};
my $password = $opts{p};

# don't die on request errors, we know what we're doing and do check results
my @mechargs = (autocheck => 0);
# disable ssl verification only if requested to
push @mechargs, (ssl_opts => { verify_hostname => 0, SSL_verify_mode => 0 }) if ($opts{N});

my $ua = WWW::Mechanize->new(@mechargs);

# get the first (form or landing) page
my $response = $ua->get($url);
if (!$ua->success)
{
	print "First page load failed: " .$response->status_line."\n";
	exit 0;
}
elsif ($ua->content !~ /loginForm/)
{
	print "First page loaded but has no matching form!\n";
	exit 0;
}

if ($opts{d})
{
	my @forms =$ua->forms();
	print STDERR "first form submission is to: ".$forms[0]->action."\n";
}
# now submit the login form
$response = $ua->submit_form(
	form_id => "loginForm",
	fields      => {
		UserName    => $username, 
		Password    => $password,
		kmsiInput => 'true',
	} );
if (!$ua->success)
{
	print "First Form submission failed: ".$response->status_line."\n";
	exit 10;
} 
# if and only if the user/password combo works, 
# do we get that msisauthenticatd cookie
# the status code doesn't reflect auth success, at all :-(
elsif ($response->headers->as_string !~ /Set-Cookie: MSISAuthenticated/)
{
	print "First Form submission succeeded but returns bad data!\n";
	exit 10;
}

if ($opts{d})
{
	my @forms =$ua->forms();
	print STDERR "second form submission is to: ".$forms[0]->action."\n";
}
# first form returns another form, which we submit to /again/
$response = $ua->submit_form(form_number => 1);
if (!$ua->success)
{
	print "Second Form submission failed: ".$response->status_line."\n";
	exit 20;
}

if ($opts{d})
{
	my @forms =$ua->forms();
	print STDERR "Third Form submission is to: ".$forms[0]->action."\n";
}

# second form /also/ returns another form
$response = $ua->submit_form(form_number => 1);
if (!$ua->success)
{
	print "Third Form submission failed: ".$response->status_line."\n";
	exit 30;
}

if ($ua->content !~ $okbody)
{
	print "Third Form submission succeeded but invalid authentication data!\n";
	exit 40;
}
else
{
	print "Authentication succeeded.\n";
	exit 100;
}

